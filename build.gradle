import com.github.spotbugs.snom.SpotBugsTask

import java.util.concurrent.TimeUnit

plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id 'com.github.spotbugs' version '6.4.8' apply false
    id 'org.owasp.dependencycheck' version '12.2.0'
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

tasks.withType(Task).configureEach {
    if (name == 'build' || name == 'assemble') {
        dependsOn 'setProjectVersions'
    }
}

// Global checkstyle file
ext.checkstyleConfigFile = new File(rootDir, "/config/checkstyle/sun_checks.xml")

subprojects {
    apply plugin: "checkstyle"
    apply plugin: "com.github.spotbugs"
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "org.owasp.dependencycheck"

    repositories {
        flatDir { dirs "lib" }
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    jacoco {
        toolVersion = '0.8.14'
    }

    if (project.name != 'tcg_rim_tool') { // run tests on every subproject except for rim_tools
        tasks.withType(Test).configureEach {
            useJUnitPlatform()
            finalizedBy(tasks.named('jacocoTestReport'))
        }
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
    }

    checkstyle {
        toolVersion = '12.3.1'
        configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
    }

    checkstyleMain {
        source = 'src/main/java'
    }

    checkstyleTest {
        source = 'src/test/java'
    }

    dependencyCheck {
        nvd {
            // Read API key from a system property 'nvd.apiKey'
            apiKey = System.getProperty("nvd.apiKey")
        }
    }

    tasks.withType(Checkstyle).configureEach {
        reports {
            xml.required = false
            html.required = true
        }
    }

    tasks.withType(SpotBugsTask).configureEach {
        excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')

        reports {
            html.required = true
        }
    }

    // Global dependency resolution strategy for all subprojects
    configurations.configureEach {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'

        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.apache.logging.log4j') {
                details.useVersion '2.25.3' // Force patched Log4j version
            }

            if (details.requested.group == 'org.apache.commons' &&
                    details.requested.name == 'commons-lang3') {
                details.useVersion '3.20.0'  // Force patched Commons Lang version
            }

            if (details.requested.group == 'org.apache.httpcomponents' &&
                    details.requested.name == 'httpclient') {
                details.useVersion '4.5.14'  // Force patched Apache Http Client version
            }
        }
    }
}

def projectVersion = rootProject.file('VERSION').text.trim()
project.ext["projVersion"] = projectVersion

tasks.register('retrieveGitHash', Exec) {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'

    // Set up the ByteArrayOutputStream to capture the output
    def outputStream = new ByteArrayOutputStream()
    standardOutput = outputStream

    doLast {
        // Store the Git hash as an extension property in the project
        ext.gitHash = outputStream.toString().trim()
    }
}

// Define a task that depends on retrieveGitHash and sets global properties
tasks.register('setProjectVersions') {
    dependsOn 'retrieveGitHash'  // Ensure retrieveGitHash runs first

    // Access the captured Git hash after retrieveGitHash has finished
    doLast {
        // Get the current time in milliseconds
        Date latestDate = new Date()
        long buildTimeInSecs = TimeUnit.MILLISECONDS.toSeconds(latestDate.getTime())

        // Set the global properties
        project.ext["jarVersion"] = "${projectVersion}.${buildTimeInSecs}.${retrieveGitHash.gitHash}"
        project.ext["packageVersion"] = "${projectVersion}.${buildTimeInSecs}.${retrieveGitHash.gitHash}.el8"

        // Print the values to verify
        println "projVersion: ${project.ext["projVersion"]}"
        println "jarVersion: ${project.ext["jarVersion"]}"
        println "packageVersion: ${project.ext["packageVersion"]}"
    }
}