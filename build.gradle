import com.github.spotbugs.snom.SpotBugsTask

import java.util.concurrent.TimeUnit

plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id 'com.github.spotbugs' version '6.4.8' apply false
    id 'org.owasp.dependencycheck' version '12.2.0'
    id 'com.palantir.git-version' version '4.3.0'
}

// Global checkstyle file
ext.checkstyleConfigFile = new File(rootDir, "/config/checkstyle/sun_checks.xml")

subprojects {
    apply plugin: "checkstyle"
    apply plugin: "com.github.spotbugs"
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "org.owasp.dependencycheck"

    repositories {
        flatDir { dirs "lib" }
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    jacoco {
        toolVersion = '0.8.14'
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        finalizedBy(tasks.named('jacocoTestReport'))
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
    }

    checkstyle {
        toolVersion = '12.3.1'
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    }

    checkstyleMain {
        source = 'src/main/java'
    }

    checkstyleTest {
        source = 'src/test/java'
    }

    dependencyCheck {
        // Read API key from a system property 'nvd.apiKey'
        nvd.apiKey = System.getProperty("nvd.apiKey")
    }

//    Uncomment the lines right after this comment, if you wish to see all the places
//    in the application that are using deprecated features
//    tasks.withType(JavaCompile).configureEach {
//        options.deprecation = true
//    }

    tasks.withType(Javadoc).configureEach {
        if (project.name == 'HIRS_AttestationCA') {
            // Remove the generated files from the source set
            source = source.filter { file ->
                !file.path.contains('build/generated/source/proto/main/java')
            }
        }
        options.addStringOption('Xmaxwarns', '0')  // Show unlimited warnings todo
    }

    tasks.withType(Checkstyle).configureEach {
        reports {
            xml.required = false
            html.required = true
        }
    }

    tasks.withType(SpotBugsTask).configureEach {
        excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')

        reports {
            html.required = true
        }
    }

    // Global dependency resolution strategy for all subprojects
    configurations.configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'

        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.apache.logging.log4j') {
                details.useVersion '2.25.3' // Force patched Log4j version
            }

            if (details.requested.group == 'org.apache.commons' &&
                    details.requested.name == 'commons-lang3') {
                details.useVersion '3.20.0'  // Force patched Commons Lang version
            }

            if (details.requested.group == 'org.apache.httpcomponents' &&
                    details.requested.name == 'httpclient') {
                details.useVersion '4.5.14'  // Force patched Apache Http Client version
            }

            if (details.requested.group == 'org.assertj' &&
                    details.requested.name == 'assertj-core') {
                details.useVersion '3.27.7'  // Force patched Assert Core version
            }
        }
    }
}

def projectVersion = rootProject.file('VERSION').text.trim()
project.ext["projVersion"] = projectVersion

def details = versionDetails()
def gitHash = details.gitHash
long buildTimeInSecs = TimeUnit.MILLISECONDS.toSeconds(new Date().getTime())

project.ext["jarVersion"] = "${projectVersion}.${buildTimeInSecs}.${gitHash}"
project.ext["packageVersion"] = "${projectVersion}.${buildTimeInSecs}.${gitHash}.el8"