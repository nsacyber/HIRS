import com.github.spotbugs.snom.SpotBugsTask

import java.util.concurrent.TimeUnit

plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id 'checkstyle'
    id 'com.github.spotbugs' version '6.0.13' apply false
    id 'org.owasp.dependencycheck' version '11.1.1'
    id 'java'
    id 'jacoco'
}

// Global checkstyle file
ext.checkstyleConfigFile = new File(rootDir, "/config/checkstyle/sun_checks.xml")

subprojects {
    apply plugin: "com.github.spotbugs"
    apply plugin: "java"
    apply plugin: "checkstyle"
    apply plugin: "org.owasp.dependencycheck"
    apply plugin: "jacoco"

    repositories {
        flatDir { dirs "lib" }
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    jacoco {
        toolVersion = '0.8.12'
    }

    if (project.name != 'tcg_rim_tool') // run tests on every subproject except for rim_tools
        test {
            useJUnitPlatform() // Use JUnit platform
            finalizedBy jacocoTestReport // Generate the JaCoCo report after running tests
        }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
    }

    checkstyle {
        toolVersion = '10.20.0'
        configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
    }

    checkstyleMain {
        source = 'src/main/java'
    }
    checkstyleTest {
        source = 'src/test/java'
    }

    tasks.withType(Checkstyle).configureEach {
        reports {
            xml.required = false
            html.required = true
        }
    }

    spotbugs {
        excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')
    }

    tasks.withType(SpotBugsTask).configureEach {
        reports {
            html.required = true
        }
    }
}

dependencies {
    repositories {
        // Use Maven Central for resolving dependencies.
        mavenCentral()
    }
}

def projectVersion = rootProject.file('VERSION').text.trim()

def buildTime = { ->
    Date latestdate = new Date()
    def time = latestdate.getTime()
    long seconds = TimeUnit.MILLISECONDS.toSeconds(time)
    return seconds
}

def gitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

project.ext["projVersion"] = "${projectVersion}"
project.ext["jarVersion"] = "${projectVersion}.${buildTime}.${gitHash}"
project.ext["packageVersion"] = "${projectVersion}.${buildTime}.${gitHash}.el8"

