import com.github.spotbugs.snom.SpotBugsTask

import java.util.concurrent.TimeUnit

plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id 'com.github.spotbugs' version '6.0.13' apply false
    id 'org.owasp.dependencycheck' version '12.2.0'
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

// Global checkstyle file
ext.checkstyleConfigFile = new File(rootDir, "/config/checkstyle/sun_checks.xml")

subprojects {
    apply plugin: "checkstyle"
    apply plugin: "com.github.spotbugs"
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "org.owasp.dependencycheck"

    repositories {
        flatDir { dirs "lib" }
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    jacoco {
        toolVersion = '0.8.14'
    }

    if (project.name != 'tcg_rim_tool') { // run tests on every subproject except for rim_tools
        tasks.withType(Test).configureEach {
            useJUnitPlatform()
            finalizedBy(tasks.named('jacocoTestReport'))
        }
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
    }

    checkstyle {
        toolVersion = '12.3.1'
        configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
    }

    checkstyleMain {
        source = 'src/main/java'
    }

    checkstyleTest {
        source = 'src/test/java'
    }

    dependencyCheck {
        nvd {
            // Read API key from a system property 'nvd.apiKey'
            apiKey = System.getProperty("nvd.apiKey")
        }
    }

    tasks.withType(Javadoc).configureEach {
        options.addStringOption('Xmaxwarns', '0')  // Show unlimited warnings todo
    }

    tasks.withType(Checkstyle).configureEach {
        reports {
            xml.required = false
            html.required = true
        }
    }

    tasks.withType(SpotBugsTask).configureEach {
        excludeFilter = file('config/spotbugs/spotbugs-exclude.xml')

        reports {
            html.required = true
        }
    }

    // Global dependency resolution strategy for all subprojects
    configurations.configureEach {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'

        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.apache.logging.log4j') {
                details.useVersion '2.25.3' // Force patched Log4j version
            }

            if (details.requested.group == 'org.apache.commons' &&
                    details.requested.name == 'commons-lang3') {
                details.useVersion '3.20.0'  // Force patched Commons Lang version
            }

            if (details.requested.group == 'org.apache.httpcomponents' &&
                    details.requested.name == 'httpclient') {
                details.useVersion '4.5.14'  // Force patched Apache Http Client version
            }
        }
    }
}

def projectVersion = rootProject.file('VERSION').text.trim()

def buildTime = { ->
    Date latestdate = new Date()
    def time = latestdate.getTime()
    long seconds = TimeUnit.MILLISECONDS.toSeconds(time)
    return seconds
}

def gitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

project.ext["projVersion"] = "${projectVersion}"
project.ext["jarVersion"] = "${projectVersion}.${buildTime}.${gitHash}"
project.ext["packageVersion"] = "${projectVersion}.${buildTime}.${gitHash}.el8"

