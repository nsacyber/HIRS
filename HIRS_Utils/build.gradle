// Get version from main project gradle
def packVersion = properties.get("packageVersion")
def jarVersion = properties.get("jarVersion")

configurations {
    configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
    jaxb
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.apache.logging.log4j') {
            details.useVersion '2.25.3'
        }
        if (details.requested.group == 'org.assertj' &&
                details.requested.name == 'assertj-core') {
            details.useVersion '3.27.7'
        }
    }
}

dependencies {
    implementation libs.authlete.cbor
    implementation libs.bouncycastle
    implementation libs.commons.codec
    implementation libs.commons.lang3
    implementation libs.commons.io
    implementation libs.glassfish.jakarta.json
    implementation libs.glassfish.jaxb.runtime
    // explicitly include the gson library to override the older version of gson
    // found in the libs.authlete.cbor dependency
    implementation libs.gson
    implementation libs.guava
    implementation libs.jackson.cbor
    implementation libs.jackson.core
    implementation libs.jackson.databind
    implementation libs.jakarta.persistence.api
    implementation libs.jakarta.xml
    implementation libs.spring.boot.starter.log4j2
    implementation libs.minimal.json
    implementation libs.nimbus.jwt

    implementation libs.pci
    // explicitly include the patched version of the apache http client dependency
    implementation libs.apacheHttpClient

    compileOnly libs.lombok
    annotationProcessor libs.lombok

    testImplementation libs.spring.boot.starter.test
    testRuntimeOnly libs.junit.platform.launcher

    testImplementation project(path: ':HIRS_AttestationCA')

    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Class-Path': configurations.runtimeClasspath.files.collect { it.toURI().toString() }.join(' ')
        )
    }
    //jar name format: [archiveBaseName]-[archiveAppendix]-[archiveVersion]-[archiveClassifier].[archiveExtension]
    archiveVersion = jarVersion

    from(sourceSets.test.resources.srcDirs) {
        into('/')
    }
}

//task generateXjcLibrary(type:Exec) {
//    workingDir 'config'
//
//    commandLine './genXjcLibrary.sh'
//}
//compileJava.dependsOn generateXjcLibrary